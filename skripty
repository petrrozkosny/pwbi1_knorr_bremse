01 suma srazek = 

-- scenar1: Respektuji filtry vizualu, zadne nerusim, nepridavam. Pouzivam zakladni agregacni funkce
-- typu SUM(),MIN()
SUM(data[srazky])

__________________________

02 suma srazek ruzyne = 
-- suma srazek v danem roce v lokalite ruzyne

-- scenar2: Respektuji filtry vizualu a nastavuji dalsi, tj. pouziji kombinaci funkci
-- SUMX(FILTER()), MINX(FILTER()), MAXX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data se vsemi jejimi filtry
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
        data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
    data[srazky])
    
    ___________________________________
    
    03 suma razek vsechny roky = 

-- scenar3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji kombinaci funkci
-- SUMX(ALL()),MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data se vsemi jejimi hodnotami,
    -- tj. tabulka neni filtrovana
    ALL(data),
--krok2: Funkce SUMX() scita sloupec srazky tabulky vracene funkci ALL()
data[srazky])

_________________________________________________


04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MINX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    -- krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec rok tabulky vracene funkci ALL()
    data[rok] = 2000),
--krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
data[srazky])

_______________________________

05 suma srazek mesic 6 = 
SUMX(FILTER(data,data[mesic] = 6),data[srazky])

__________________________________

06 maximalni srazky rok = 

MAX(data[srazky])

__________________________________________________

07 podil srazek = 

-- podil srazek v cervnu v ruzyni v danem roce na celkovych srazkach ruzyne v danem roce

-- suma srazek v ruzyni v danem roce v mesici 6
SUMX(
    FILTER(data,data[mesic] = 6 && data[lokalita] = "RUZYNE"),
    data[srazky])
-- deleno
/
-- suma srazek v ruzyni v danem roce

SUMX(
    FILTER(data,data[lokalita]="RUZYNE"),
data[srazky])

___________________________

08 podil ruzyne na srazkach = 

-- suma srazek v danem roce v ruzyni
SUMX(FILTER(data,data[lokalita] = "RUZYNE"),data[srazky])
-- deleno
/
-- suma srazek
SUM(data[srazky])

___________________________________________-

09 prvni datum kdy prselo = 
-- v danem roce, minimální datum, kdy srážky > 0
MINX(
    FILTER(data,data[srazky] >0),
data[datum])

__________________________________________________________________

10 pocet destivych dnu ruzyne = 
-- v danem roce, lokalite ruzyne, kdy srazky > 0
countx(
    FILTER(data,data[srazky] > 0 && data[lokalita] = "RUZYNE"),
data[datum])

____________________________________________________

11 suma srazek porovnani = 
-- suma srazek v danem mesici a roce vs. suma srazek v danem mesici
-- a minimalnim roce daneho mesice
VAR suma_srazek = SUM(data[srazky])
VAR mesic = SELECTEDVALUE(data[mesic])
VAR minimalni_rok = MINX(FILTER(ALL(data),data[mesic]=SELECTEDVALUE(data[mesic])),data[rok])
VAR suma_srazek_min_rok  = SUMX(FILTER(ALL(data),data[mesic]=mesic && data[rok] = minimalni_rok),data[srazky])
RETURN suma_srazek  - suma_srazek_min_rok

______________________________________________

12 barva lokality = 
VAR lokalita = SELECTEDVALUE(data[lokalita])
VAR zeme = CALCULATE(FIRSTNONBLANK(dim_oblasti[země],TRUE()),FILTER(dim_oblasti,dim_oblasti[stanice] = lokalita))
return 
SWITCH(zeme,
"Rakousko","GOLD",
"Cesko","TOMATO",
"Slovensko", "CYAN")

_______________________________________________

13 nazev grafu = 
 
 
 SWITCH(TRUE(),
 HASONEFILTER(data[lokalita]), "Vývoj ročních max. teplot v lokalitě "&SELECTEDVALUE(data[lokalita]),
 ISFILTERED(data[lokalita]),
 "Vývoj ročních max. teplot v lokalitách: "&
 CONCATENATEX(DISTINCT(data[lokalita]),data[lokalita],", "),
 "Vývoj ročních max. teplot ve všech lokalitách")
